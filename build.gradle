buildscript {
  repositories {
    mavenCentral()
    maven { url 'https://s01.oss.sonatype.org' }
    gradlePluginPortal()
    mavenLocal()
    google()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:8.2.1'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

  }
}

plugins {
  id 'org.barfuin.gradle.taskinfo' version '2.1.0'
}

allprojects {
  apply plugin: 'eclipse'
  apply plugin: 'idea'
  apply from: rootProject.file('gradle/assets.gradle')
  apply from: rootProject.file('gradle/version.gradle')

  // This allows you to "Build and run using IntelliJ IDEA", an option in IDEA's Settings.
  idea {
    module {
      outputDir file('build/classes/java/main')
      testOutputDir file('build/classes/java/test')
    }
  }
}

configure(subprojects.findAll { it.group != "rectball.android" }) {
  apply plugin: 'java-library'
  apply plugin: 'kotlin'
  sourceCompatibility = 17
  compileJava {
    options.incremental = true
  }
  compileKotlin {
    compilerOptions.jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17)
  }
  kotlin {
    jvmToolchain(17)
  }
}

configure(project(':core')) {
  compileJava.finalizedBy assetsList
  compileJava.finalizedBy assetsVersion
  assetsList.dependsOn assetsVersion
}

subprojects {
  version = project.ext.appVersion
  ext.appName = 'Rectball'
  repositories {
    mavenCentral()
    maven { url 'https://s01.oss.sonatype.org' }
    // You may want to remove the following line if you have errors downloading dependencies.
    mavenLocal()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://teavm.org/maven/repository/' }
  }
}

eclipse.project.name = 'rectball'
