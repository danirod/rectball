buildscript {
  repositories {
    mavenCentral()
    maven { url 'https://s01.oss.sonatype.org' }
    gradlePluginPortal()
    mavenLocal()
    google()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:8.2.1'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

  }
}

allprojects {
  apply plugin: 'eclipse'
  apply plugin: 'idea'

  // This allows you to "Build and run using IntelliJ IDEA", an option in IDEA's Settings.
  idea {
    module {
      outputDir file('build/classes/java/main')
      testOutputDir file('build/classes/java/test')
    }
  }
}

configure(subprojects.findAll { it.group != "rectball.android" }) {
  apply plugin: 'java-library'
  apply plugin: 'kotlin'
  sourceCompatibility = 17
  compileJava {
    options.incremental = true
  }
  // Generated files in assets (https://lyze.dev/2021/04/29/libGDX-Internal-Assets-List/)
  // Piggybacking this solution to also generate version information from Gradle and pack
  // it into the game.
  compileJava.doLast {
    def assetsFolder = new File("${project.rootDir}/assets/")
    def assetsFile = new File(assetsFolder, "assets.txt")
    def versionFile = new File(assetsFolder, "version.properties")

    // delete that file in case we've already created it
    assetsFile.delete()
    versionFile.delete()

    // Generate version file information
    def versionProperties = new Properties()
    versionProperties['appVersion'] = rectballVersion
    versionProperties['appFullVersion'] = rectballVersionFull
    versionProperties['appBuildNumber'] = rectballBuildNumber
    versionFile.withWriter { w -> versionProperties.store(w, null) }

    // Iterate through all files inside that folder and pack it into assets.txt
    fileTree(assetsFolder).collect { assetsFolder.relativePath(it) }.each {
      assetsFile.append(it + "\n")
    }
  }
  compileKotlin {
    compilerOptions.jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17)
  }

  kotlin {
    jvmToolchain(17)
  }
}

subprojects {
  version = rectballVersionFull
  ext.appName = 'Rectball'
  repositories {
    mavenCentral()
    maven { url 'https://s01.oss.sonatype.org' }
    // You may want to remove the following line if you have errors downloading dependencies.
    mavenLocal()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://teavm.org/maven/repository/' }
  }
}

eclipse.project.name = 'rectball'
